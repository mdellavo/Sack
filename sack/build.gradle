apply plugin: 'com.android.library'

group = 'org.quuux.sack'
version = '0.2'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"

    resourcePrefix "sack__"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 3
        versionName "0.2.1"
    }

    lintOptions {
        abortOnError false
    }
}



install {

    repositories.mavenInstaller {
        pom {
            project {

                name 'sack'
                description 'A simple data store for Android'
                url 'https://github.com/mdellavo/Sack'
                inceptionYear '2015'

                packaging 'aar'
                groupId 'org.quuux.sack'
                artifactId 'sack'
                version '0.2.1'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    connection 'https://github.com/mdellavo/Feller.git'
                    url 'https://github.com/mdellavo/Feller'

                }
                developers {
                    developer {
                        name 'Marc DellaVolpe'
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'sack'
        userOrg = "mdellavo"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mdellavo/Sack.git'
        version {
            name = '0.2.1'
            desc = 'A simple data store for Android'
            vcsTag = '0.2.1'
            released  = new Date()
        }
    }
}

dependencies {
    implementation 'com.android.support:support-v4:27.0.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}

